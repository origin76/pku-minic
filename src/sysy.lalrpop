use crate::ast::*;
use std::boxed::Box;
use koopa::ir::BinaryOp;


// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
    // 跳过空白符和单行注释
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]*" => {},
    // 处理块注释
    r"/\*([^*]|\*+[^*/])*\*+/" => {},
    // 剩下的情况采用默认方式处理
    _
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <stmt: Stmt> "}" => Block { <> };

Stmt: Stmt = {
  "return" <exp: Exp> ";" => Stmt::Return(*exp),
};

pub Exp: Box<Exp> = AddExp;

AddExp: Box<Exp> = {
    <l:AddExp> "+" <r:MulExp> => Box::new(Exp::BinaryExp(l, BinaryOp::Add, r)),
    <l:AddExp> "-" <r:MulExp> => Box::new(Exp::BinaryExp(l, BinaryOp::Sub, r)),
    MulExp, // 递归出口：如果不是加减，就把它当做乘除表达式处理
}

MulExp: Box<Exp> = {
    <l:MulExp> "*" <r:UnaryExp> => Box::new(Exp::BinaryExp(l, BinaryOp::Mul, r)),
    <l:MulExp> "/" <r:UnaryExp> => Box::new(Exp::BinaryExp(l, BinaryOp::Div, r)),
    <l:MulExp> "%" <r:UnaryExp> => Box::new(Exp::BinaryExp(l, BinaryOp::Mod, r)),
    UnaryExp, // 递归出口
}

PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Parentheses(<>),
  <num: Number> => PrimaryExp::Number(<>),
};

UnaryExp: Box<Exp> = {
    <op:UnaryOp> <e:UnaryExp> => Box::new(Exp::UnaryExp(op, e)),
    <p:PrimaryExp> => Box::new(Exp::PrimaryExp(Box::new(p))),
}

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Plus,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Not,
};


Number: i32 = <num: IntConst> => <>;

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}
